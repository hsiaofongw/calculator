PRODUCTION RULES:

S      ->  S'  CMP_0        # 开始语句，将可能直接推导为一个值或者一个 CMP_0 判断语句

CMP_0  ->  ==  S'  CMP_0    # 在 CMP_0 状态下，当遇到一个 == 符号时，将开始匹配相等性判断语句
        |  ε                # 如果没有符号可匹配，那就不匹配，翻译器对于 CMP_0 -> ε 会什么都不做

S'     ->  A                # 值可以是一个数组
        |  CMP_1            # 值可以是布尔代数运算或者常规算术运算的结果
        |  str              # 值可以是一个字符串

A      ->  {  L  }          # 符号 A 一定要推导为一个列表，而列表一定以左花括号 { 开头

L      ->  S  L'            # 一个列表可以是非空的
        |  ε                # 一个列表还可以是空的

L'     ->  ,  S  L'         # 一个列表如果是非空的，那么一定是逗号分隔的
        |  ε                # 一个列表如果是非空的，那么最后一项不会是逗号

CMP_1  ->  E  CMP_2         # 表达式可以参与到布尔代数运算中来，并且布尔算符与操作数的结合优先级是比普通四则运算算符低的，这也就是说：先做算术运算，后做更一般的代数运算

CMP_2  ->  >  E  CMP_2      # 这条规则将当前运算结果以严格大于布尔算符 > 作为算符与下一项进行运算
        |  <  E  CMP_2      # 这条规则将当前运算结果以严格小于布尔算符 < 作为算符与下一项进行运算
        |  >=  E  CMP_2     # 这条规则将当前运算结果以严格不小于布尔算符 >= 作为算符与下一项进行运算
        |  <=  E  CMP_2     # 这条规则将当前运算结果以严格不大于布尔算符 <= 作为算符与下一项进行运算
        |  ε                # 这条规则代表布尔表达式匹配结束

E      ->  T  E'            # 一个表达式由两部分构成，首项 T 和余项 E'

E'     ->  +  T  E'         # 余项 E' 可以以加号 + 开头，代表当前计算结果以相加的形式与下一项进行运算
        |  -  T  E'         # 余项 E' 可以以减号 - 开头，代表当前计算结果以相减的形式与下一项进行运算
        |  ε                # 如果没有符号可匹配，那就不匹配，翻译器对于 E' -> ε 会什么都不做

T      ->  REM_0  T'        # 一个项由两部分构成，首项、余项 T'

T'     ->  *  REM_0  T'     # 余项 T' 可以以乘号 * 开头，代表当前计算结果以相乘的形式与下一项进行运算
        |  /  REM_0  T'     # 余项 T' 可以以除号 / 开头，代表当前计算结果以相除的形式与下一项进行运算
        |  ε                # 如果没有符号可匹配，那就不匹配，翻译器对于 T' -> ε 会什么都不做

REM_0  ->  NEG REM_1        # 余数运算开始

REM_1  ->  % NEG REM_1      # 将当前结果与下一个数进行取余数运算
        |  ε                # 余数运算结束

NEG    ->  -  POW_0          # 一个数前面加负号可成为一个负数
        |  POW_0             # 一个数前面也可不加负号

POW_0  ->  F  POW_1         # 幂次运算开始

POW_1  ->  ^  F  POW_1      # 将当前结果与下一个数进行幂次运算
        |  ε                # 幂次运算结束

F      ->  (  E  )          # 一个因子 F 可以推导为一个被括号对包住的东西，这时向前看符号一定是左括号 (
        |  num              # 一个因子 F 可以推导为一个数字，显然这需要向前看符号为数字 token 也就是 num 才能匹配
        |  id  P            # 一个因子 F 可以推导为一个标识符 id, 显然这需要向前看符号为标识符 token 也就是 id 时才能匹配
         
P      ->  [  L  ]  P'      # 符号 P 可以被推导为一个被方括号包围的串，并且最左边一定是左方括号，这时，翻译器将会将栈中的标识符弹出，然后制造一个函数表达式节点，再将函数表达式节点弹回去
        |  =  S             # 符号 P 可以被推导为一个以单等于号开头的串，翻译器在翻译应用了这样的产生式的节点时，会将栈顶的值弹出，以弹出值作为左值节点，然后对 S 节点求值并将它作为右值节点弹出，最后再弹回一个 Assign 节点到栈完事
        |  ε                # 如果没有符号可匹配，那就不匹配，翻译器对于 P -> ε 会什么都不做
        
P'     ->  =  S             # 符号 P' 可以被推导为一个以单等于号开头的串，翻译器在翻译应用了这样的产生式的节点时，会将栈顶的值弹出，以弹出值作为左值节点，然后对 S 节点求值并将它作为右值节点弹出，最后再弹回一个 Assign 节点到栈完事
        |  ε                # 如果没有符号可匹配，那就不匹配，翻译器对于 P' -> ε 会什么都不做
