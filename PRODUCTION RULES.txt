PRODUCTION RULES:

JSON -> OBJ | ARY                      # 开始符号 JSON, JSON 可以是一个对象 (OBJ) 或者数组 (ARY)
OBJ -> { KV_LIST }                     # 一个对象从一个左花括号 { 开始，右花括号 } 结束，花括号对中间包围着键-值对列表 (KV_LIST)
ARY -> [ VAL_LIST ]                    # 一个数组从一个左方括号 [ 开始，右方括号 ] 结束，方括号对中间包围着值列表 (VAL_LIST)
VAL -> ATOM | JSON                     # 值可以是原子值 (ATOM) 和 JSON
ATOM -> str | BOOL | num | null        # 原子值 (ATOM) 有字符串 (str)、布尔值 (bool) 或者数 (num)
VAL_LIST -> VAL VAL_LIST_E | eps       # 值列表 (VAL_LIST) 可以有 0 个或者多个值
VAL_LIST_E -> , VAL VAL_LIST_E | eps   # 值列表以逗号 , 作为分隔符
KV -> str : VAL                        # 一个键值以一串字符 str 开头，然后是一个冒号 :，然后是一个值 (VAL)
KV_LIST -> KV KV_LIST_E | eps          # 一个键值对列表 (KV_LIST) 里边可以有 0 个或者多个键值对 (KV)
KV_LIST_E -> , KV KV_LIST_E | eps      # 键值对列表是以逗号 , 作为分隔符的
BOOL -> true | false                   # 布尔值
